!  parimp em Assembly as88
!  versao usando readnum
!
!  (c) rro, 2015-02-24
!
!  para montar o programa:
!    $ ./as88 parimp2.s readnum.s
!
!  para executar o programa:
!    $ ./s88 parimp2
!
!  para depurar o programa:
!    $ ./t88 parimp2

_EXIT	   =	  1	! seven system call numbers
_READ	   =	  3
_WRITE	   =	  4
_OPEN	   =	  5
_CLOSE	   =	  6
_CREAT	   =	  8
_LSEEK	   =	 19
_GETCHAR   =	117	! five system subroutine numbers
_SPRINTF   =	121
_PUTCHAR   =	122
_SSCANF	   =	125
_PRINTF	   =	127
STDIN	   =	  0	! three initially opened file descriptors
STDOUT	   =	  1
STDERR	   =	  2

.SECT .TEXT		! Definition of section header labels
TxtSecHd:
.SECT .DATA
DatSecHd:
.SECT .TEXT

.SECT .TEXT

start:
	MOV	BP,SP		! salva SP
	PUSH	pr1		! endereco da string
	PUSH	_PRINTF		! no. da syscall
	SYS			! executa syscall 

	CALL	readnum		! le string e converte para numero
	MOV	(num),AX	! armazena o numero lido em 'num'

	MOV	DX,0		! dividendo=DX:AX - DX=0, AX=num
        MOV     CX,2            ! divisor em CX
        IDIV    CX              ! quociente em AX, resto em DX
        CMP     DX,0		! compara DX com 0
        JE      imp_par         ! se for igual, e' par

imp_impar:
	PUSH	str_impar	! 2o argumento de printf() e' "impar"
        JMP     imprime
	
imp_par: 
	PUSH    str_par		! 2o argumento de printf() e' "par"
	
imprime:
	PUSH	pr2		! 1o argumento de printf() e' pr2
	PUSH	_PRINTF
	SYS

encerra:    
	MOV	SP,BP		! limpa pilha
	PUSH	0		! codigo de retorno=0
	PUSH	_EXIT		
	SYS			! encerra o programa
fim:
	
.SECT	.DATA
pr1:		.ASCIZ	'\nEntre um numero: '
str_impar:	.ASCIZ	'impar'
str_par:	.ASCIZ	'par'
pr2:		.ASCIZ	'O numero eh %s\n'

.SECT	.BSS
num:	.SPACE 30
numf:	.SPACE 1
! subrotina para ler um numero da entrada padrao em as88
! - numero lido e' retornado em AX
! - nao e' verificado se o numero lido cabe em 16 bits
!
! (c) rro, 2015-02-24

_RNBSIZ=8			! tamanho do buffer de leitura

.SECT	.TEXT
	
readnum:
	PUSH	BP		! salva BP
	MOV	BP,SP		! salva SP em BP
	PUSH	_RNBSIZ		! no. de bytes a ler
	PUSH	_rnbuf		! endereco da string
	PUSH	STDIN		! arquivo de entrada
	PUSH	_READ		! no. da syscall
	SYS			! executa syscall 

	PUSH	_rnum		! onde sera' armazenado o numero
	PUSH	_rnssf		! string de formato
	PUSH	_rnbuf		! string lida
	PUSH	_SSCANF
	SYS

	MOV	AX,(_rnum)	! guarda o numero em AX
	MOV	SP,BP		! limpa a pilha
	POP	BP		! restaura BP
	RET			! encerra funcao
	
.SECT	.DATA
_rnbuf:	.SPACE	_RNBSIZ
_rnssf:	.ASCIZ	'%d'
_rnum:	.WORD	0
	
.SECT	.BSS
